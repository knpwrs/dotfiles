# Executes commands at login pre-zshrc.
# Based on https://github.com/sorin-ionescu/prezto/blob/a84ac5b0023d71c98bb28a68c550dc13f6c51945/runcoms/zprofile

# Open files in GUI
if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

# Editors
export EDITOR='nvim'
export VISUAL='nvim' # Editor to be opened from $PAGER

if type bat > /dev/null; then
  export PAGER=('bat', '-p')
  export MANPAGER="sh -c 'col -bx | bat -plman'"
else
  export PAGER='less -R'
fi

# Language
if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

# Paths
## Ensure path arrays do not contain duplicates (except manpath, which needs gnuman placed at the start)
typeset -gU cdpath fpath mailpath path

if hash brew 2>/dev/null; then
  BREW_PREFIX=$(brew --prefix)
elif [ -x /opt/homebrew/bin/brew ]; then
  BREW_PREFIX=$(/opt/homebrew/bin/brew --prefix)
else
  BREW_PREFIX=""
fi

export WHALEBREW_INSTALL_PATH=$HOME/.whalebin

if [[ -n "$BREW_PREFIX" ]]; then
  path=(
    $BREW_PREFIX/opt/coreutils/libexec/gnubin # GNU coreutils
    $BREW_PREFIX/opt/gnu-sed/libexec/gnubin # GNU sed
    $BREW_PREFIX/opt/gnu-tar/libexec/gnubin # GNU tar
    $BREW_PREFIX/opt/gnu-which/libexec/gnubin # GNU which
    $BREW_PREFIX/opt/make/libexec/gnubin # GNU make
    $BREW_PREFIX/bin
    $path
  )

  # GNU coreutils man path fix
  manpath=(
    $BREW_PREFIX/opt/coreutils/libexec/gnuman
    $BREW_PREFIX/share/man
    $manpath
  )
fi

path=(
  $HOME/bin # Custom home bin directory
  $HOME/.bin # bin directory from this repo
  $HOME/.local/bin
  $WHALEBREW_INSTALL_PATH
  $path
)

# rust
RUST_BIN="$HOME/.cargo/bin"
if [[ -d "$RUST_BIN" ]]; then
  path=(
    $RUST_BIN
    $path
  )
fi

# bun
BUN_BIN="$HOME/.bun/bin"
if [[ -d "$BUN_BIN" ]]; then
  path=(
    $BUN_BIN # bun
    $path
  )
  [ -s "$BUN_BIN/_bun" ] && source "$BUN_BIN/_bun"
fi

# Temporary Files
if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$USER"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"
if [[ ! -d "$TMPPREFIX" ]]; then
  mkdir -p "$TMPPREFIX"
fi

# History
export HISTSIZE=500
